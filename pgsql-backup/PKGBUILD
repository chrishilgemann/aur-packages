# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages

pkgname=pgsql-backup
pkgver=0.9.11
pkgrel=2
pkgdesc="Automatic rolling backups for PostgreSQL"
arch=('any')
url="http://fukawi2.nl/index.php?page=code-pgsql-backup"
license=('GPL')
depends=('tar' 'coreutils' 'gzip' 'postgresql')
makedepends=('perl') # reqd for pod2man to make man page
backup=("etc/$pkgname.conf")
optdepends=('gzip: compression of backups'
            'bzip2: better compression of backups'
            'xz: even better compression of backups')
install="$pkgname.install"
### this assumes there is a tag in the git tree
### matching $pkgver (which there should be)
source=("$pkgname-$pkgver.tar.gz::https://github.com/fukawi2/$pkgname/tarball/v$pkgver")
noextract=("$pkgname-$pkgver.tar.gz") # see prepare()
md5sums=('8ea27514e7dfbfd7516f6cd712f2e478')

prepare() {
  # the tarball comes with part of the commit sha1
  # forming part of the directory name, so to have
  # a deterministic tree when building, we'll extract
  # the tarball ourselves, stripping the leading
  # directory name
  mkdir "$srcdir"/$pkgname-$pkgver/ || rm -Rf "$srcdir"/$pkgname-$pkgver/*
  cd "$srcdir"/$pkgname-$pkgver/
  tar xzf "$srcdir"/$pkgname-$pkgver.tar.gz --strip-components=1

  sed -i Makefile \
      -e 's|pod2man|/usr/bin/core_perl/pod2man|g' \
      -e 's|D_BIN=/usr/local/sbin|D_BIN=/usr/bin|g'
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/

  make DESTDIR="$pkgdir" PREFIX='/usr' install
}

# vim:set ts=2 sw=2 et:
