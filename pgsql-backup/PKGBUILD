# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages

pkgname=pgsql-backup
pkgver=0.9.8
pkgrel=1
pkgdesc="Automatic rolling backups for PostgreSQL"
arch=('any')
url="http://fukawi2.nl/index.php?page=code-pgsql-backup"
license=('GPL')
depends=('tar' 'coreutils' 'gzip' 'postgresql-client')
makedepends=('perl') # reqd for pod2man to make man page
backup=("etc/$pkgname.conf")
optdepends=('bzip2: better compression of backups')
install="$pkgname.install"
### this assumes there is a tag in the git tree
### matching $pkgver (which there should be)
source=("$pkgname-$pkgver.tar.gz::https://github.com/fukawi2/$pkgname/tarball/v$pkgver")
noextract=("$pkgname-$pkgver.tar.gz") # see build()
md5sums=('928fc042654b80fa6550c48d499d93c3')

build() {
  # the tarball comes with part of the commit sha1
  # forming part of the directory name, so to have
  # a deterministic tree when building, we'll extract
  # the tarball ourselves, stripping the leading
  # directory name
  mkdir "$srcdir"/$pkgname-$pkgver/ || rm -Rf "$srcdir"/$pkgname-$pkgver/*
  cd "$srcdir"/$pkgname-$pkgver/
  tar xzf "$srcdir"/$pkgname-$pkgver.tar.gz --strip-components=1

  # build the man pages
  /usr/bin/core_perl/pod2man --name=pgsql-backup pgsql-backup.pod pgsql-backup.1.man
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/

  # main script
  install -D -m0755 -o root -g root src/$pkgname.sh "$pkgdir"/usr/bin/$pkgname

  # config file
  install -D -m0600 -o root -g root $pkgname.conf "$pkgdir"/etc/$pkgname.conf

  # man page
  install -Dm0644 pgsql-backup.1.man "$pkgdir"/usr/share/man/man1/pgsql-backup.1p
}

# vim:set ts=2 sw=2 et:
