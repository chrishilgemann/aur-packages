# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Christoph Zeiler <rabyte*gmail>

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=firetable
pkgver=4.9
pkgrel=4
pkgdesc="An iptables firewall script"
arch=('any')
url="http://projects.leisink.org/$pkgname"
license=('GPL')
depends=('iptables' 'perl')
backup=(etc/firetable/{config.eth0,firetable.conf})
install=$pkgname.install
source=(http://projects.leisink.org/files/$pkgname-$pkgver.tar.gz rc.$pkgname)
md5sums=('74ee94117bf7b572f29ba1ce476a9f56'
         '1a883577ba7b543d7447ab9577bd4663')

build() {
	msg "Nothing to compile for $pkgname"
}

package() {
  cd $pkgname-$pkgver
  
  # fix path to iptables
  sed -i 's|"/sbin/iptables"|"/usr/sbin/iptables"|' $pkgname
  
  # Install configuration files
  install -Dm640 etc/firetable/config.eth0 $pkgdir/etc/firetable/config.eth0
  install -Dm640 etc/firetable/firetable.conf $pkgdir/etc/firetable/firetable.conf
  
  # Install executable and rc script
  install -Dm755 $pkgname $pkgdir/usr/sbin/$pkgname
  install -Dm755 $srcdir/rc.$pkgname $pkgdir/etc/rc.d/$pkgname
  
  # Install man page
  install -Dm644 man/firetable.1 $pkgdir/usr/share/man/man1/firetable.1
}

# vim:set ts=2 sw=2 et:
