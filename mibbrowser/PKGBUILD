# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages

pkgname=mibbrowser
pkgver=1
pkgrel=1
pkgdesc="A powerful and easy-to-use MIB browser. Supports standard & proprietary MIBs. Also issue SNMP requests to retrieve agent data. Built-in trap receiver can receive SNMP traps and handle trap storm."
arch=('any')
license=('custom')
depends=('java-runtime')
url="http://ireasoning.com/mibbrowser.shtml"
source=("http://www.ireasoning.com/download/mibfree/$pkgname.zip")
md5sums=('325ff16d029e695e5f8d28474359f6e8')

build() {
	msg "Nothing to compile for $pkgname"
}

package() {
  _dest="opt/${pkgname}"

  # Licenses first, before we move the whole directory to opt
  cd "$srcdir/ireasoning/${pkgname}"
  for F in license.txt THIRDPARTYLICENSEREADME.txt ; do
    # Install to /usr and remove from distribution dir
    install -D -m644 $F "${pkgdir}/usr/share/licenses/${pkgname}/$F"
    rm -f $F
    # Symlink the original file from /opt to /usr
    ln -sf /usr/share/licenses/${pkgname}/$F $F
  done

  # Install the compiled app to /opt
  cd "$srcdir/ireasoning"
  mkdir -p "$pkgdir/opt/"
  cp -r ${pkgname} $pkgdir/opt/

  # Create a symlink to /usr/bin
  mkdir -p "$pkgdir/usr/bin/"
  ln -sf /${_dest}/browser.sh "$pkgdir/usr/bin/$pkgname"
  chmod 755 "${pkgdir}/${_dest}/browser.sh"

  # Log file needs to be writeable by all users
  touch "$pkgdir/${_dest}/log/log.txt"
  chmod 666 "$pkgdir/${_dest}/log/log.txt"

  # Fix the run script
  sed -e "s|^DIRNAME=.*\$|DIRNAME='/${_dest}';|g" \
    -i "${pkgdir}/${_dest}/browser.sh"
}

# vim:set ts=2 sw=2 et:
