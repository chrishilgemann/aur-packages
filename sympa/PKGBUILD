# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# Contributor: Dimitri Pekarovsky <dimitri dot pekarovsky where gmail dot com>

pkgname=sympa
pkgver=6.1.17
pkgrel=1
pkgdesc="Sympa is a mailing list management software"
url="http://www.sympa.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=(
  # Available in standard repos
  'perl-archive-zip' 'perl-dbd-mysql' 'perl-dbi' 'perl-crypt-openssl-bignum'
  'perl-crypt-openssl-rsa' 'perl-crypt-openssl-random' 'perl-fcgi'
  'perl-file-copy-recursive' 'perl-io-stringy' 'perl-html-formattext'
  'perl-mail-dkim' 'perl-mailtools' 'perl-net-dns' 'perl-soap-lite'
  'perl-term-progressbar' 'perl-template-toolkit' 'perl-xml-libxml'

  # Available in AUR
  'perl-html-stripscripts-parser' 'perl-digest-sha' 'perl-Encode'
  'perl-crypt-ciphersaber' 'perl-file-temp' 'perl-mime-base64'
  'perl-mime-charset' 'perl-mime-encwords' 'perl-net-netmask'
  'perl-regexp-common' 'perl-test-harness'
)
install="${pkgname}.install"
source=("http://www.sympa.org/distribution/$pkgname-$pkgver.tar.gz"
        'sympa.rc')
md5sums=('8c7e1fd71cb60514b052cbf59faa262e'
         '33862041b6f8bbda9a2ebeddcbdafe91')

_prefix='/srv/sympa'

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  export PATH=$PATH:/usr/lib/perl5/core_perl/bin

  ./configure \
    --prefix=${_prefix} \
    --sysconfdir=${_prefix}/etc \
    --with-confdir=${_prefix}/etc \
    --with-sendmail_aliases=${_prefix}/etc/mail/aliases \
    --with-virtual_aliases=${_prefix}/etc/mail/virtual \
    --with-perl=`/usr/bin/which perl`

  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  # we don't need init.d files
  rm -rf "$pkgdir/etc/rc.d/init.d"

  install -d "$pkgdir/${_prefix}/var/lock/subsys"

  # TODO: systemd?
  install -D -m755 "$srcdir/sympa.rc" "$pkgdir/etc/rc.d/sympa"

  chown -R 52:52 "$pkgdir/${_prefix}"
}

# vim:set ts=2 sw=2 et:
