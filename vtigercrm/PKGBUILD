# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=vtigercrm
pkgver=5.4.0
pkgrel=1
pkgdesc="A free, full-featured, 100% Open Source CRM software ideal for small and medium businesses"
arch=('any')
url='https://www.vtiger.com/crm/open-source/'
license=('custom')
depends=('apache>=2.0.40' 'mysql>=4.1' 'php>=5.4' 'php-apache' 'php-gs' 'gd'
         'perl' 'perl-archive-zip' 'libpng' 'libjpeg' 'freetype2' 'zlib')
backup=("srv/http/$pkgname/config.db.php"
        "srv/http/$pkgname/config.inc.php"
        "srv/http/$pkgname/config.performance.php"
        "srv/http/$pkgname/config.php"
        "srv/http/$pkgname/connection.php")
options=('!strip')
install=$pkgname.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        "http-$pkgname.conf"
        'PHPv54-v2.patch')
md5sums=('4d9fb047c42a8231bcbbad9c5e31292e'
         'fbb511340a85cd80442beef70c142c42'
         'dea518ebc093ff67f9fdc70caad969b9')

_INSTDIR="srv/http/${pkgname}"
_HTTPDIR='etc/httpd/conf/extra'
  
prepare() {
  cd $srcdir/$pkgname/

  # patch to make vtigercrm play nice with PHP 5.4.0
  # thanks to jplona
  # refer: http://trac.vtiger.com/cgi-bin/trac.cgi/ticket/7618
  patch -p1 < $srcdir/PHPv54-v2.patch
}

package() {
  cd $srcdir/$pkgname/

  # Install the server files
  msg "Installing http files to $_INSTDIR then setting ownership and permissions"
  install -dm755 ${pkgdir}/$_INSTDIR
  cp -ra ./ ${pkgdir}/$_INSTDIR
  chown -R 33:33 ${pkgdir}/$_INSTDIR
  chmod -R u=rwX,go=rX ${pkgdir}/$_INSTDIR
  
  # Install the Apache config file
  msg "Installing Apache config file"
  install -Dm644 -o33 -g33 $srcdir/http-$pkgname.conf ${pkgdir}/$_HTTPDIR/httpd-$pkgname.conf

  # Install the custom license
  msg "Installing licences to $pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
