# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=vtigercrm
pkgver=5.2.1
pkgrel=1
pkgdesc="A free, full-featured, 100% Open Source CRM software ideal for small and medium businesses"
arch=('any')
url="http://vtiger.com"
license=('custom')
depends=('apache>=2.0.40' 'mysql>=4.1' 'php>=5.0' 'gd' 'perl' 'perl-archive-zip' 'libpng' 'libjpeg' 'freetype2' 'zlib')
backup=("srv/http/$pkgname/config.db.php"
        "srv/http/$pkgname/config.inc.php"
        "srv/http/$pkgname/config.performance.php"
        "srv/http/$pkgname/config.php"
        "srv/http/$pkgname/connection.php")
options=('!strip')
install=$pkgname.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        "http-$pkgname.conf")
md5sums=('3d028918fffce148dcedae48744d5979'
         'fbb511340a85cd80442beef70c142c42')

package() {
  _INSTDIR="${pkgdir}/srv/http/${pkgname}"
  _HTTPDIR="${pkgdir}/etc/httpd/conf/extra"
  
  # Install the server files
  msg "Installing http files to $_INSTDIR then setting ownership and permissions"
  install -dm755 $_INSTDIR
  cp -ra $srcdir/$pkgname/* $_INSTDIR
  chown -R 33:33 $_INSTDIR
  chmod -R u=rwX,go=rX $_INSTDIR
  
  # Install the Apache config file
  msg "Installing Apache config file"
  install -Dm644 -o33 -g33 $srcdir/http-$pkgname.conf $_HTTPDIR/httpd-$pkgname.conf

  # Install the custom license
  msg "Installing licences to $pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 $srcdir/$pkgname/LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt
  install -Dm644 $srcdir/$pkgname/LICENSE_AGREEMENT.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE_AGREEMENT.txt
}

# vim:set ts=2 sw=2 et:
