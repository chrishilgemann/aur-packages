# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: György Balló <ballogy@freestart.hu>
# Contributor: Adam <adam@russ.co.il>

pkgname=gnome-web-photo
pkgver=0.9
pkgrel=3
pkgdesc="a tool to generate full-size image files and thumbnails from HTML files and web pages"
url="http://git.gnome.org/browse/gnome-web-photo/"
license=("LGPL")
arch=('i686' 'x86_64')
depends=('xulrunner' 'gconf' 'pkg-config')
makedepends=('intltool>=0.35.0')
install="$pkgname.install"
source=("http://ftp.gnome.org/pub/gnome/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('62952330a62b463df5fef315f4a7cb93')

build() {
  cd $srcdir/$pkgname-$pkgver

  # Thx to György Balló
  sed -e 's/--cflags-only-I libxul-unstable libxul/--cflags-only-I libxul/' \
    -i configure
  sed -e 's/RenderDocument(cutout, PR_FALSE, PR_TRUE,/RenderDocument(cutout, nsIPresShell::RENDER_IGNORE_VIEWPORT_SCROLLING,/' \
    -i src/Writer.cpp
  
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/bin \
    --disable-static \
    --disable-schemas-install \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  
  make DESTDIR=$pkgdir install
  
  #install -dm755 $pkgdir/usr/share/gconf/schemas || return 1
  #gconf-merge-schema $pkgdir/usr/share/gconf/schemas/$pkgname.schemas $pkgdir/etc/gconf/schemas/*.schemas || return 1
  #rm -f $pkgdir/etc/gconf/schemas/*.schemas || return 1
}

# vim:set ts=2 sw=2 et:
