# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: György Balló <ballogy@freestart.hu>
# Contributor: Adam <adam@russ.co.il>

pkgname=gnome-web-photo
pkgver=0.10
pkgrel=1
pkgdesc="a tool to generate full-size image files and thumbnails from HTML files and web pages"
url="http://git.gnome.org/browse/gnome-web-photo/"
license=("LGPL")
arch=('i686' 'x86_64')
depends=('libwebkit' 'gconf' 'pkg-config')
makedepends=('intltool>=0.35.0')
install="$pkgname.install"
source=("http://ftp.gnome.org/pub/gnome/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('42a1043a8937849dee023eea500143fb')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Thx to György Balló
  sed -e 's/--cflags-only-I libxul-unstable libxul/--cflags-only-I libxul/' \
      -i configure

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/bin \
    --with-gtk=2.0 \
    --disable-static \
    --disable-schemas-install \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
