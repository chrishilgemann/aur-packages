# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Nathan Owe <ndowens04 at gmail>

pkgname=freeipmi
pkgver=1.0.3
pkgrel=2
pkgdesc="sensor monitoring, system event monitoring, power control, and serial-over-LAN (SOL)"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/freeipmi/"
license=('GPL')
depends=('libgcrypt')
options=('!libtool')
source=("http://ftp.gluster.com/pub/freeipmi/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('e3d73925bec75f902abfe905bbb828e7')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --infodir=/usr/share/info

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir/ install

  # Move init scripts and config to Arch paths
  mv $pkgdir/etc/init.d $pkgdir/etc/rc.d
  mv $pkgdir/etc/sysconfig $pkgdir/etc/conf.d

  # Fix references to 'init.d' and 'sysconfig'
  sed -e 's|/etc/init.d/|/etc/rc.d/|g' \
      -e 's|/etc/rc.d/init.d/functions|/etc/rc.d/functions|g' \
      -i $pkgdir/etc/rc.d/* \
      -i $pkgdir/etc/logrotate.d/*
  sed -e 's|CONFFILE=/etc/sysconfig/bmc-watchdog|CONFFILE=/etc/cond.f/bmc-watchdog|g' \
      -i $pkgdir/etc/rc.d/bmc-watchdog
  
  rm -Rf $pkgdir/usr/share/info/dir
}

# vim:set ts=2 sw=2 et:
