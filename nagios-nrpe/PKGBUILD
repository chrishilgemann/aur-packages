# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# Maintainer: Dale Blount <dale@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=nagios-nrpe
pkgver=2.12
pkgrel=7
pkgdesc="Daemon to answer plugin requests from Nagios on remote hosts."
arch=(i686 x86_64)
license=('GPL')
depends=('openssl')
options=(!libtool)
install="$pkgname.install"
backup=('etc/nrpe/nrpe.cfg')
url="http://www.nagios.org/"
source=("http://downloads.sourceforge.net/nagios/nrpe-$pkgver.tar.gz" 'rc.nrpe')
md5sums=('b2d75e2962f1e3151ef58794d60c9e97'
         '8be4e2d5869cf7866a6a26427b71824d')

build() {
	cd $srcdir/nrpe-$pkgver

	./configure --prefix=/usr \
    --sysconfdir=/etc/nrpe \
		--with-nrpe-user=nrpe \
    --with-nrpe-group=nrpe \
		--enable-command-args || return 1
  
	make || return 1
	
}

package() {
  cd $srcdir/nrpe-$pkgver
  
  # install sample config file
  install -D -m644 sample-config/nrpe.cfg $pkgdir/etc/nrpe/nrpe.cfg || return 1
  
  # comment out default samples as they won't work and may lessen security
  sed -i 's/^command/#command/' $pkgdir/etc/nrpe/nrpe.cfg || return 1
  
  # install main application
  install -D -m755 src/nrpe $pkgdir/usr/sbin/nrpe || return 1
  
  # install xinetd template
  install -Dm644 sample-config/nrpe.xinetd $pkgdir/etc/xinetd.d/nrpe || return 1
  sed \
    -e 's|disable         = no|disable         = yes|g' \
    -e 's|/usr/bin/nrpe|/usr/sbin/nrpe|g' \
    -i $pkgdir/etc/xinetd.d/nrpe || return 1
  
  # install rc init script
  install -D -m755 $srcdir/rc.nrpe $pkgdir/etc/rc.d/nrpe || return 1
}

# vim:set ts=2 sw=2 et:
