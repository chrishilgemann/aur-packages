# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>

pkgname=nagios-nsca
pkgver=2.7.2
pkgrel=2
pkgdesc="Integrate passive alerts and checks from remote machines and applications with Nagios. Useful for processing security alerts, as well as redundant and distributed Nagios setups."
arch=(i686 x86_64)
license=('GPL')
depends=(openssl)
options=(!libtool)
install="$pkgname.install"
backup=(etc/nsca/nsca.cfg etc/nsca/send_nsca.cfg)
url="http://www.nagios.org/"
source=(http://downloads.sourceforge.net/nagios/nsca-$pkgver.tar.gz
	rc.nsca)
md5sums=('33a98e7975f633a9489d7a8938ed6131'
         'cd3b5d3de096ee89395f9ca961039bdd')

build() {
	cd $srcdir/nsca-$pkgver

	./configure --prefix=/usr \
    --sysconfdir=/etc/nsca \
		--with-nsca-user=nsca \
    --with-nsca-grp=nsca || return 1

	make all || return 1
}

package() {
	cd $srcdir/nsca-$pkgver

	install -D -m640 sample-config/nsca.cfg $pkgdir/etc/nsca/nsca.cfg || return 1
	install -D -m640 sample-config/send_nsca.cfg $pkgdir/etc/nsca/send_nsca.cfg || return 1
	install -D -m755 src/nsca $pkgdir/usr/sbin/nsca || return 1
	install -D -m755 src/send_nsca $pkgdir/usr/sbin/send_nsca || return 1
	install -D -m755 $srcdir/rc.nsca $pkgdir/etc/rc.d/nsca || return 1 
}

# vim:set ts=2 sw=2 et:
