# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=nagios-nsca
pkgver=2.9.1
pkgrel=2
pkgdesc="Integrate passive alerts and checks from remote machines and applications with Nagios. Useful for processing security alerts, as well as redundant and distributed Nagios setups."
arch=('i686' 'x86_64')
license=('GPL')
depends=('openssl' 'libmcrypt')
options=(!libtool)
install="$pkgname.install"
backup=('etc/nsca/nsca.cfg' 'etc/nsca/send_nsca.cfg')
url="http://www.nagios.org/"
source=("http://downloads.sourceforge.net/nagios/nsca-$pkgver.tar.gz"
        'rc.nsca'
        'systemd.nsca')
md5sums=('3fe2576a8cc5b252110a93f4c8d978c6'
         'cd3b5d3de096ee89395f9ca961039bdd'
         'ce3f8804a5723f508e8cb24171eb39bb')

build() {
	cd $srcdir/nsca-$pkgver

  # on my system at least, the values set by makepkg.conf for these 2 env vars
  # causes the build to fail. unset them and let the configure script figure
  # out how it would like them.
  unset CFLAGS LDFLAGS

	./configure \
    --prefix=/usr \
    --sysconfdir=/etc/nsca \
		--with-nsca-user=nsca \
    --with-nsca-grp=nsca

  # make will fail with parallel jobs, so need to use -j1
	make all
}

package() {
	cd $srcdir/nsca-$pkgver

	install -D -m640 sample-config/nsca.cfg $pkgdir/etc/nsca/nsca.cfg
	install -D -m640 sample-config/send_nsca.cfg $pkgdir/etc/nsca/send_nsca.cfg
	install -D -m755 src/nsca $pkgdir/usr/sbin/nsca
	install -D -m755 src/send_nsca $pkgdir/usr/sbin/send_nsca
	install -D -m755 $srcdir/rc.nsca $pkgdir/etc/rc.d/nsca

  # install systemd "unit file"
  install -D -m755 $srcdir/systemd.nsca $pkgdir/usr/lib/systemd/system/nsca.service
}

# vim:set ts=2 sw=2 et:
