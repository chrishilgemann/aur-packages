# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=zabbix-server
pkgver=1.8.5
pkgrel=4
pkgdesc="Software for monitoring of your applications, network and servers."
arch=('i686' 'x86_64')
url="http://www.zabbix.com"
license=('GPL')
depends=('apache' 'postgresql' 'php' 'php-pgsql' 'php-gd' 'fping' 'net-snmp' 'curl' 'iksemel')
install=("$pkgname.install")
options=('emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/zabbix/zabbix-$pkgver.tar.gz"
        'zabbix_server.conf' 'zabbix_trapper.conf' 'zabbix_proxy.conf' 'rc.zabbix-server')
md5sums=('58b9253fb0eace1e20b2fe5c1fce32a3'
         '26b0401a83bdb1dce29338e5b2786620'
         '9832a81e134c8e2c11e2a06b7adbf88f'
         '0310b92afb3f35c1075fff53db737212'
         '97ff007a91957b1954ab59149a8b373e')

_HTMLPATH='srv/http/zabbix'

build() {
  cd "$srcdir/zabbix-$pkgver"

  # Only one database can be compiled for; Uncomment/Comment the
  # --with-pgsql/--with-mysql lines below to suit your requirements
  ./configure \
    --prefix=/usr \
    --enable-server \
    --with-net-snmp \
    --with-jabber \
    --with-libcurl \
    --with-pgsql
    #--with-mysql

  make
}

package() {
  cd "$srcdir/zabbix-$pkgver"

  make DESTDIR="$pkgdir" install

  # Create data dirs required
  install -d -m0750 $pkgdir/var/run/zabbix
  install -d -m0750 $pkgdir/var/log/zabbix

  # sql templates
  for sql_file in ibm_db2.sql mysql.sql oracle.sql postgresql.sql sqlite.sql ; do
    install -D -m 0444 create/schema/$sql_file $pkgdir/usr/share/zabbix/dbms/create/schema/$sql_file
  done
  for sql_file in data.sql images_ibm_db2.sql images_mysql.sql images_oracle.sql images_pgsql.sql images_sqlite3.sql ; do
    install -D -m 0444 create/data/$sql_file $pkgdir/usr/share/zabbix/dbms/create/data/$sql_file
  done
  cp -r create/data/images/ $pkgdir/usr/share/zabbix/dbms/create/data/images

  # frontends (user:group = root:apache)
  mkdir -p $pkgdir/$_HTMLPATH/
  cp -r frontends/php/* $pkgdir/$_HTMLPATH/
  chown -R 0:33 $pkgdir/$_HTMLPATH/
  chmod -R u=rwX,g=rX,o= $pkgdir/$_HTMLPATH/

  # default configuration files
  install -D -m 0640 $srcdir/zabbix_server.conf $pkgdir/etc/zabbix/zabbix_server.conf
  install -D -m 0640 $srcdir/zabbix_trapper.conf $pkgdir/etc/zabbix/zabbix_trapper.conf

  # init script
  install -D -m 0755 $srcdir/rc.zabbix-server $pkgdir/etc/rc.d/$pkgname
}

# vim:set ts=2 sw=2 et:
