# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

pkgname=zabbix-server
pkgver=1.8.5
pkgrel=1
pkgdesc="Software for monitoring of your applications, network and servers."
arch=('i686' 'x86_64')
url="http://www.zabbix.com"
license=('GPL')
depends=('apache' 'postgresql' 'php' 'php-pgsql' 'php-gd' 'fping')
install=("$pkgname.install")
options=('!emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/zabbix/zabbix-$pkgver.tar.gz"
        'zabbix_server.conf' 'zabbix_trapper.conf' 'zabbix_proxy.conf' 'rc.zabbix-server')
md5sums=('58b9253fb0eace1e20b2fe5c1fce32a3'
         '26b0401a83bdb1dce29338e5b2786620'
         '9832a81e134c8e2c11e2a06b7adbf88f'
         '0310b92afb3f35c1075fff53db737212'
         '97ff007a91957b1954ab59149a8b373e')

_HTMLPATH='srv/http/zabbix'

build() {
  cd "$srcdir/zabbix-$pkgver"

  ./configure --prefix=/usr --enable-server --with-pgsql

  make
}

package() {
  cd "$srcdir/zabbix-$pkgver"

  make DESTDIR="$pkgdir" install

  # Create data dirs required
  install -d -m0750 $pkgdir/var/run/zabbix
  install -d -m0750 $pkgdir/var/log/zabbix

  # sql templates
  install -D -m 0755 create/data/data.sql $pkgdir/usr/share/zabbix/dbms/create/data/data.sql
  install -D -m 0755 create/data/images_mysql.sql $pkgdir/usr/share/zabbix/dbms/create/data/images_mysql.sql
  install -D -m 0755 create/schema/mysql.sql $pkgdir/usr/share/zabbix/dbms/create/mysql.sql

  install -d -m 0755 -o 0 -g 33 $pkgdir/$_HTMLPATH/
  cp -r frontends/php/* $pkgdir/$_HTMLPATH/

  # default configuration files
  install -D -m 0640 $srcdir/zabbix_server.conf $pkgdir/etc/zabbix/zabbix_server.conf
  install -D -m 0640 $srcdir/zabbix_trapper.conf $pkgdir/etc/zabbix/zabbix_trapper.conf

  # init script
  install -D -m 0755 $srcdir/rc.zabbix-server $pkgdir/etc/rc.d/zabbix-server/$pkgname
}

# vim:set ts=2 sw=2 et:
