# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages

pkgname=mirrorbrain
pkgver=2.13.0
pkgrel=1
pkgdesc="an open source framework to run a content delivery network using mirror servers."
arch=('i686' 'x86_64')
url="http://mirrorbrain.org"
license=('GPL')
depends=('apache>=2.2.6' 'apache-mod_form' 'apache-mod_geoip2' 'geoip'
         'perl' 'perl-config-ini' 'perl-libwww' 'perl-dbd-pg' 'perl-digest-md4' 'perl-timedate'
         'python' 'python-psycopg2' 'python-sqlobject' 'cmdln')
makedepends=('apache>=2.2.6' 'apache-mod_form' 'python')
optdepends=('postgresqli-server: recommended database, but can use any database the Apache DBD API supports'
            'mod_asn: refined mirror selection and full exploitation of network locality')
provides=('mod_mirrorbrain')
backup=("etc/$pkgname.conf")
install="$pkgname.install"
source=("http://mirrorbrain.org/files/releases/$pkgname-$pkgver.tar.gz"
        "$pkgname.conf")
md5sums=('a68b0c81fc18ff3ea752890ed0abb799'
         '490bcf829ca6cc8086f3d18bddb9420c')

build() {
  cd "$srcdir/$pkgname-$pkgver/mod_mirrorbrain/"
  apxs -c mod_mirrorbrain.c

  cd "$srcdir/$pkgname-$pkgver/tools"
  gcc -Wall -o geoiplookup_continent geoiplookup_continent.c -lGeoIP
  gcc -Wall -o geoiplookup_city geoiplookup_city.c -lGeoIP
}

package() {
  # Install the apache module
  cd "$srcdir/$pkgname-$pkgver/mod_mirrorbrain/"
  install -Dm755 .libs/mod_mirrorbrain.so $pkgdir/usr/lib/httpd/modules/mod_mirrorbrain.so || return 1
  
  # Install SQL templates
  cd "$srcdir/$pkgname-$pkgver/sql/"
  mkdir -p $pkgdir/usr/share/$pkgname/sql/ || return 1
  cp -rv * $pkgdir/usr/share/$pkgname/sql/ || return 1

  # Install default configuration file
  cd "$srcdir/"
  install -Dm640 -o root -g 33 $pkgname.conf $pkgdir/etc/$pkgname.conf || return 1

  # Install the mb script
  cd "$srcdir/$pkgname-$pkgver/mb/"
  python setup.py install --root=$pkgdir || return 1
  ln -sf /usr/bin/mirrordoctor.py $pkgdir/usr/bin/mb || return 1

  # Install the tools  
  cd "$srcdir/$pkgname-$pkgver/tools/"
  install -Dm755 geoiplookup_continent $pkgdir/usr/bin/geoiplookup_continent
  install -Dm755 geoiplookup_city      $pkgdir/usr/bin/geoiplookup_city
  install -Dm755 geoip-lite-update     $pkgdir/usr/bin/geoip-lite-update
  install -Dm755 null-rsync            $pkgdir/usr/bin/null-rsync
  install -Dm755 scanner.pl            $pkgdir/usr/bin/scanner

  # Install the mirrorprobe script
  cd "$srcdir/$pkgname-$pkgver/mirrorprobe/"
  install -Dm0755 mirrorprobe.py $pkgdir/usr/bin/mirrorprobe || return 1
}

# vim:set ts=2 sw=2 et:
